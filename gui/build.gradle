plugins {
    id 'java'
    id "idea"
    id "org.jetbrains.compose"
    id "org.jetbrains.kotlin.jvm"
}

group 'me.liuli.fluidity.gui'

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

configurations.all {
    attributes {
        attribute(Attribute.of("ui", String.class), "awt")
    }
}

repositories {
    maven {
        url = "https://ayanoyuugiri.github.io/compose-j8/"
        content {
            includeGroupByRegex "org\\.jetbrains\\.compose.*"
            includeGroupByRegex "org\\.jetbrains\\.skiko.*"
        }
    }
    maven {
        url = "https://maven.pkg.jetbrains.space/public/p/compose/dev"
        content {
            includeModuleByRegex ("org\\.jetbrains\\.skiko.*", "skiko-awt-runtime-.*")
        }
    }
    mavenCentral()
}

String targetOs = "linux"
if (System.getProperty("os.name").startsWith("Win")) {
    targetOs = "windows"
} else if (System.getProperty("os.name") == "Mac OS X") {
    targetOs = "macos"
}
String targetArch = "x64"
if (System.getProperty("os.arch") == "aarch64") {
    targetArch = "arm64"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation project(":core")

    // compose
    implementation "org.jetbrains.skiko:skiko-awt:$skiko_version"
    implementation("org.jetbrains.compose.desktop:desktop-jvm:$compose_version") {
        exclude module: "kotlin-stdlib"
        exclude module: "kotlin-stdlib-common"
        exclude module: "kotlin-stdlib-jdk8"
        exclude module: "skiko"
    }
    implementation "org.jetbrains.compose.ui:ui-util:$compose_version"

    runtimeOnly "org.jetbrains.skiko:skiko-awt-runtime-$targetOs-$targetArch:0.7.32"

    implementation MinecraftDependencyKt.minecraftDep(project, "core/src/main/resources/fluidity_at.cfg")
}

compose.desktop.application {
}